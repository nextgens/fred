buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath files('lib/gradle-witness.jar')
    }
}

configurations {
    provided
    compile
}

apply plugin: 'witness'
apply plugin: 'java'

repositories {
    jcenter()
    mavenLocal() // TODO: use lib/ instead?
    ivy {
        url 'https://downloads.freenetproject.org/'
        layout 'pattern', {
            artifact '/latest/[module][revision].[ext]'
        }
    }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
    test {
        java {
            srcDir 'test/'
        }
    }
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

def gitrev
task buildInfo {
    try {
        def cmd = "git describe --always --abbrev=4 --dirty"
        def proc = cmd.execute()
        gitrev = proc.text.trim()
    } catch (java.io.IOException e) {
        gitrev = "@unknown@"
    }
}

jar {
    manifest {
        attributes("Permissions": "all-permissions")
        attributes("Application-Name": "Freenet REference Daemon")
        attributes("Required-Ext-Version": 29)
        attributes("Recommended-Ext-Version": 29)
        attributes([
                       "Specification-Title": "Freenet",
                       "Specification-Version": "0.7.5",
                       "Specification-Vendor": "freenetproject.org",
                       "Implementation-Title": "Freenet",
                       "Implementation-Version": "0.7.5 ${gitrev}",
                       "Implementation-Vendor": "freenetproject.org",
                   ], "common")
    }
}
jar.dependsOn buildInfo

task copyResourcesToClasses << {
    copy {
        from "${projectDir}/test/freenet/client/filter/"
        into "${buildDir}/classes/test/freenet/client/filter/"
        include 'png/**'
        include 'bmp/**'
        include 'mp3/**'
    }
    copy {
        from "${projectDir}/test/freenet/crypt/ciphers/"
        into "${buildDir}/classes/test/freenet/crypt/ciphers/"
        include 'rijndael-gladman-test-data/**'
    }

    copy {
        from "${projectDir}/test/freenet/"
        into "${buildDir}/classes/test/freenet/"
        include 'l10n/*properties'
    }
}
processTestResources.dependsOn copyResourcesToClasses

// In this section you declare the dependencies for your production and test code
dependencies {
    compile "org.bouncycastle:bcprov-jdk15on:1.54"
    compile ("org.freenetproject:freenet-ext@jar") {
        artifact {
            name = "freenet-ext"
            extension = "jar"
            type = "jar"
            url = "https://downloads.freenetproject.org/latest/freenet-ext.jar"
        }
    }

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-all:1.9.5"
    testCompile "org.hamcrest:hamcrest-library:1.3"
}

dependencyVerification {
    // testCompile includes all of compile deps... so let's include only these
    includedConfigurations = [configurations.testCompile]
    verify = [
        'org.bouncycastle:bcprov-jdk15on:d0ae14598f9c528d2ab7bb8ed00e785a5440f692712cd362d69328aba25efb57',
        'org.freenetproject:freenet-ext:32f2b3d6beedf54137ea2f9a3ebef67666d769f0966b08cd17fd7db59ba4d79f',
        'junit:junit:59721f0805e223d84b90677887d9ff567dc534d7c502ca903c0c2b17f05c116a',
        'org.mockito:mockito-all:b2a63307d1dce3aa1623fdaacb2327a4cd7795b0066f31bf542b1e8f2683239e',
        'org.hamcrest:hamcrest-library:711d64522f9ec410983bd310934296da134be4254a125080a0416ec178dfad1c',
        'org.hamcrest:hamcrest-core:66fdef91e9739348df7a096aa384a5685f4e875584cce89386a7a47251c4d8e9',
    ]
}
